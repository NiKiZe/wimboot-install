#!ipxe

set nloc ${netX/busloc}
set pciid ${pci/${nloc}.0.2}${pci/${nloc}.2.2}

# clear any loaded files (avoids push into the wim)
imgfree
kernel wimboot gui rawbcd                                                          || shell
initrd --name BCD                win10/BCD                      BCD                || shell
initrd --name boot.sdi           win10/boot.sdi                 boot.sdi           || shell
iseq ${pciid} 808615bb && goto use_e1d68 ||
iseq ${pciid} 808615be && goto use_e1d68 ||
goto skip_e1d68
:use_e1d68
initrd     win10/drv/i219v_e1d68/e1d68x64.cat ||
initrd     win10/drv/i219v_e1d68/e1d68x64.inf ||
initrd     win10/drv/i219v_e1d68/e1d68x64.sys ||
initrd     win10/drv/i219v_e1d68/e1dmsg.dll ||
:skip_e1d68
iseq ${pciid} 1af41000 && goto use_virtio ||
goto skip_virtio
:use_virtio
# without extra name wimboot crashes (in pcbios mode)
initrd     win10/drv/virtio/netkvm.cat   netkvm.cat ||
initrd     win10/drv/virtio/netkvm.inf   netkvm.inf ||
initrd     win10/drv/virtio/netkvm.sys   netkvm.sys ||
initrd     win10/drv/virtio/netkvmco.dll netkvmco.dll ||
initrd     win10/drv/virtio/vioscsi.cat  vioscsi.cat ||
initrd     win10/drv/virtio/vioscsi.inf  vioscsi.inf ||
initrd     win10/drv/virtio/vioscsi.sys  vioscsi.sys ||
initrd     win10/drv/virtio/viostor.cat  viostor.cat ||
initrd     win10/drv/virtio/viostor.inf  viostor.inf ||
initrd     win10/drv/virtio/viostor.sys  viostor.sys ||
goto skip_virtio
:skip_virtio
initrd --name startnet.cmd  win10/startnet.php?mac=${netX/mac:hexraw}&pciid=${pciid}  startnet.cmd       || shell
initrd --name winpe.jpg          win10/winpe.jpg                winpe.jpg          || echo no background - ignored
initrd --name winre.jpg          win10/winpe.jpg                winre.jpg          || echo no background - ignored
initrd --name setup.bmp          win10/winpe.jpg                setup.bmp          || echo no background - ignored
# these are copied to several different places in the path on virtual VFAT that wimboot creates
initrd --name segmono_boot.ttf   win10/Fonts/segmono_boot.ttf   segmono_boot.ttf   || shell
initrd --name segoe_slboot.ttf   win10/Fonts/segoe_slboot.ttf   segoe_slboot.ttf   || shell
initrd --name segoen_slboot.ttf  win10/Fonts/segoen_slboot.ttf  segoen_slboot.ttf  || shell
initrd --name wgl4_boot.ttf      win10/Fonts/wgl4_boot.ttf      wgl4_boot.ttf      || shell
# actuall boot.wim, must be boot.wim both in EFI and PCBIOS
initrd --name boot.wim           win10/win10_boot_64_en.wim     boot.wim           || shell
boot || shell
goto start
